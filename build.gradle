plugins {
    id 'dev.architectury.loom' version '1.6-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id "org.jetbrains.kotlin.jvm" version "1.7.10"
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

// Custom task to generate changelog at root project level
task generateChangelog {
    doLast {
        def version = project.version
        def changelogFile = file("$rootDir/CHANGELOG.md")
        def changelogContent = changelogFile.exists() ? changelogFile.text : "No changelog available."
        def changelogDir = file("$rootDir/changelog")
        changelogDir.mkdirs()
        def changelogOutputFile = file("$changelogDir/CHANGELOG-${version}.md")
        changelogOutputFile.text = changelogContent
        changelogFile.write("# Changelog\n\n## [${version}]\n\n${changelogContent}", true)
    }
}

// Ensure the generateChangelog task runs before the build task in all subprojects
subprojects {
    apply plugin: "java"
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'
    apply plugin: "org.jetbrains.kotlin.jvm"

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.mod_version/$rootProject.archives_name-$project.name"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        mavenCentral()
        maven {
            name = "sonatype-oss-snapshots1"
            url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://maven.impactdev.net/repository/development/"
        }
        maven {
            url = uri("https://cursemaven.com")
            content {
                includeGroup("curse.maven")
            }
        }
        mavenCentral()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    // Ensure generateChangelog runs before build
    tasks.named("build") {
        dependsOn(rootProject.tasks.generateChangelog)
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
                // Attach the generated changelog as an artifact
                artifact("${rootProject.buildDir}/changelog/CHANGELOG-${rootProject.version}.md") {
                    classifier "changelog"
                }
            }
        }

        repositories {
            // Add repositories to publish to here.
        }
    }
}
